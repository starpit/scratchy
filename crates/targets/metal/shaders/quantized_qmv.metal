// SPDX-License-Identifier: Apache-2.0
//
// Faithful port of MLX `affine_qmv_quad` / `affine_qmv_fast` /
// `affine_qmv` decode-matvec kernels from
// `mlx/backend/metal/kernels/quantized.h` (lines 692-975 for the
// `*_impl` helpers, 1444-1597 for the `[[kernel]]` entry points,
// 28-392 for the qdot / load_vector helpers, 1351-1387 for
// `adjust_matrix_offsets`).
//
// Per `INT4_PARITY_PLAN.md` §P3, instantiations cover:
//   bits = 4
//   group_size in {32, 64, 128}
//   dtype in {f16, bf16}
//   qmv_quad: D in {64, 128} × batched in {0, 1}
//   qmv_fast: batched in {0, 1}
//   qmv:      batched in {0, 1}
//
// Symbol naming follows the existing scratchy-target-metal precedent
// (`affine_dequantize_<dtype>_gs_<gs>_b_<bits>`):
//   affine_qmv_quad_<dtype>_gs_<gs>_b_<bits>_d_<D>_batch_<batched>
//   affine_qmv_fast_<dtype>_gs_<gs>_b_<bits>_batch_<batched>
//   affine_qmv_<dtype>_gs_<gs>_b_<bits>_batch_<batched>
//
// The helper templates (load_vector / qdot / etc.) keep the full
// `bits in {2,3,4,5,6,8}` switch so the body is byte-identical to
// MLX. Only bits=4 is instantiated; other branches dead-code under
// the constexpr template arg.

#include <metal_simdgroup>
#include <metal_stdlib>
#include "baked.h"
#include "gated_act.h"
#include "moe_route.h"

using namespace metal;

#define MLX_MTL_CONST static constant constexpr const

MLX_MTL_CONST int SIMD_SIZE = 32;
MLX_MTL_CONST int QUAD_SIZE = 4;

// ─────────────────────────────────────────────────────────────────
// `AffineQmvConstants`, compiled in: the K/N dims MLX passes as
// setBytes runtime args, folded into each kernel the bake compiles.
// ─────────────────────────────────────────────────────────────────

SCRATCHY_CONSTANT(int, IN_VEC_SIZE,  0);
SCRATCHY_CONSTANT(int, OUT_VEC_SIZE, 1);
// 5: the 4-bit codes are stored XOR 0x88 (signed q - 8; `AffineCodes::Offset8`,
// set on matrix-unit tapes, where the W4A8 prefill GEMM reads them as int4).
// XOR-ing each loaded word restores the unsigned codes. Unset: as written.
SCRATCHY_CONSTANT_OPTIONAL(bool, AFFINE_CODES_OFFSET8, 5);
constant constexpr uint16_t AFFINE_CODES_XOR = AFFINE_CODES_OFFSET8 ? 0x8888 : 0;
// 8 / 9: x is an RMSNorm's input (`qmv_fast_impl`'s gain, its eps and weight offset): each lane
// dots x ⊙ gain, and the row's sum of squares scales the dot once at the end. 10: the row adds
// into y, a residual stream, rounded as the matvec stores it and as the add stores the sum.
SCRATCHY_CONSTANT_OPTIONAL(float, QMV_NORM_EPS, 8);
SCRATCHY_CONSTANT_OPTIONAL(float, QMV_NORM_W_OFFSET, 9);
SCRATCHY_CONSTANT_OPTIONAL(bool, QMV_RESIDUAL, 10);
constant constexpr bool QMV_NORMED = QMV_NORM_EPS_SET;
constant constexpr float QMV_GAIN_OFFSET = QMV_NORM_W_OFFSET_SET ? QMV_NORM_W_OFFSET : 0.0f;
constant constexpr bool QMV_ADDS = QMV_RESIDUAL_SET && QMV_RESIDUAL;
// 11 / 12: the row's bias (buffer 16, added to the dot) and a scale (multiplying it after), before
// any residual add.
SCRATCHY_CONSTANT_OPTIONAL(bool, QMV_BIASED_FC, 11);
SCRATCHY_CONSTANT_OPTIONAL(float, QMV_SCALE, 12);
constant constexpr bool QMV_BIASED = QMV_BIASED_FC_SET && QMV_BIASED_FC;
constant constexpr bool QMV_SCALED = QMV_SCALE_SET;

// A lane's `count` input values as a normalizing matvec dots them: x ⊙ (gain + offset) into `xg`,
// the squares of the first `valid` into `sum_sq`.
template <typename U, int count, typename T_act, typename T_scale>
inline void qmv_normalize(
    const device T_act* x,
    const device T_scale* gain,
    thread U* xg,
    thread U& sum_sq,
    int valid) {
  for (int i = 0; i < count; i++) {
    if (i < valid) {
      const U v = x[i];
      sum_sq += v * v;
      xg[i] = v * (U(gain[i]) + QMV_GAIN_OFFSET);
    }
  }
}

// What a row's dot is scaled by: the norm's 1 / rms over the `count` values whose squares sum to
// `total_sq` under QMV_NORMED, else 1.
inline float qmv_row_scale(float total_sq, int count) {
  return QMV_NORMED ? 1.0f / sqrt(total_sq / float(count) + QMV_NORM_EPS) : 1.0f;
}

// Store row `n`'s dot `r` at `*y` in the activation type, its bias added and its scale applied
// first, then — under QMV_ADDS — added into the residual already there: the row takes one rounding.
template <typename T_act, typename Y>
inline void qmv_store(Y y, float r, const device T_act* bias, int n) {
  r = QMV_BIASED ? r + float(bias[n]) : r;
  r = QMV_SCALED ? r * QMV_SCALE : r;
  *y = static_cast<T_act>(QMV_ADDS ? float(*y) + r : r);
}

// ─────────────────────────────────────────────────────────────────
// Pack helpers — quantized.h:17-26
// ─────────────────────────────────────────────────────────────────

template <int bits, int wsize = 8>
inline constexpr short get_pack_factor() {
  return (bits == 3 || bits == 5) ? 8 : (bits == 6 ? 4 : wsize / bits);
}

template <int bits, int wsize = 8>
inline constexpr short get_bytes_per_pack() {
  constexpr int power_of_2_bits = (bits & (bits - 1)) == 0;
  return power_of_2_bits ? (wsize / 8) : (bits == 5 ? 5 : 3);
}

// ─────────────────────────────────────────────────────────────────
// NVFP4 E2M1 decode. The 4-bit code is sign-magnitude: bit 3 = sign,
// bits 0-2 = magnitude index into the E2M1 value table. Byte/nibble
// layout is identical to MLX-affine int4 (low nibble = even element),
// so the surrounding qmv/qmm_t machinery is shared verbatim; only the
// per-code dequant differs (LUT lookup vs raw integer, and no per-group
// bias). Matches Python vLLM's `kE2M1ToFloat` (`nvfp4_emulation_utils`).
// ─────────────────────────────────────────────────────────────────
// E2M1 decode computed arithmetically — NO lookup table. A file-scope
// `constant float[]` LUT compiles to a static initializer / global
// constructor (`air.static_init` / `GLOBAL__sub_I`), which the MTL4
// pipeline-compiler path does NOT run — leaving the table uninitialized
// → garbage weights under MTL4 (MTL3 / offline-via-ShaderCache runs the
// ctor, so the bug only surfaced in production, not in unit tests).
// E2M1 = 1 sign · 2 exp · 1 mantissa: mag = (e==0) ? m·0.5
// : (1 + m·0.5)·2^(e-1) → {0,.5,1,1.5,2,3,4,6} for codes 0..7,
// bit-identical to the old LUT.
inline float nvfp4_decode(uint code) {
  uint e = (code >> 1u) & 0x3u;
  uint m = code & 0x1u;
  float mant = (e == 0u) ? (float(m) * 0.5f) : (1.0f + float(m) * 0.5f);
  float scale = (e == 0u) ? 1.0f : float(1u << (e - 1u));
  float mag = mant * scale;
  return (code & 0x8u) ? -mag : mag;
}

// ─────────────────────────────────────────────────────────────────
// load_vector / load_vector_safe — quantized.h:28-189
// ─────────────────────────────────────────────────────────────────

template <typename T, typename U, int values_per_thread, int bits, typename P = const device T*>
inline U load_vector(P x, thread U* x_thread) {
  static_assert(
      bits == 2 || bits == 3 || bits == 4 || bits == 5 || bits == 6 ||
          bits == 8,
      "Template undefined for bits not in {2, 3, 4, 5, 6, 8}");

  U sum = 0;

  if (bits == 2) {
    for (int i = 0; i < values_per_thread; i += 4) {
      sum += x[i] + x[i + 1] + x[i + 2] + x[i + 3];
      x_thread[i] = x[i];
      x_thread[i + 1] = x[i + 1] / 4.0f;
      x_thread[i + 2] = x[i + 2] / 16.0f;
      x_thread[i + 3] = x[i + 3] / 64.0f;
    }
  }

  else if (bits == 3) {
    for (int i = 0; i < values_per_thread; i += 8) {
      sum += x[i] + x[i + 1] + x[i + 2] + x[i + 3] + x[i + 4] + x[i + 5] +
          x[i + 6] + x[i + 7];
      x_thread[i] = x[i];
      x_thread[i + 1] = x[i + 1] / 8.0f;
      x_thread[i + 2] = x[i + 2] / 64.0f;
      x_thread[i + 3] = x[i + 3] / 2.0f;
      x_thread[i + 4] = x[i + 4] / 16.0f;
      x_thread[i + 5] = x[i + 5] / 128.0f;
      x_thread[i + 6] = x[i + 6] / 4.0f;
      x_thread[i + 7] = x[i + 7] / 32.0f;
    }
  }

  else if (bits == 4) {
    for (int i = 0; i < values_per_thread; i += 4) {
      sum += x[i] + x[i + 1] + x[i + 2] + x[i + 3];
      x_thread[i] = x[i];
      x_thread[i + 1] = x[i + 1] / 16.0f;
      x_thread[i + 2] = x[i + 2] / 256.0f;
      x_thread[i + 3] = x[i + 3] / 4096.0f;
    }
  }

  else if (bits == 5) {
    for (int i = 0; i < values_per_thread; i += 8) {
      sum += x[i] + x[i + 1] + x[i + 2] + x[i + 3] + x[i + 4] + x[i + 5] +
          x[i + 6] + x[i + 7];
      x_thread[i] = x[i];
      x_thread[i + 1] = x[i + 1] / 32.0f;
      x_thread[i + 2] = x[i + 2] / 4.0f;
      x_thread[i + 3] = x[i + 3] / 128.0f;
      x_thread[i + 4] = x[i + 4] / 16.0f;
      x_thread[i + 5] = x[i + 5] / 2.0f;
      x_thread[i + 6] = x[i + 6] / 64.0f;
      x_thread[i + 7] = x[i + 7] / 8.0f;
    }
  }

  else if (bits == 6) {
    for (int i = 0; i < values_per_thread; i += 4) {
      sum += x[i] + x[i + 1] + x[i + 2] + x[i + 3];
      x_thread[i] = x[i];
      x_thread[i + 1] = x[i + 1] / 64.0f;
      x_thread[i + 2] = x[i + 2] / 16.0f;
      x_thread[i + 3] = x[i + 3] / 4.0f;
    }
  }

  else if (bits == 8) {
    for (int i = 0; i < values_per_thread; i++) {
      sum += x[i];
      x_thread[i] = x[i];
    }
  }

  return sum;
}

template <typename T, typename U, int values_per_thread, int bits, typename P = const device T*>
inline U load_vector_safe(P x, thread U* x_thread, int N) {
  static_assert(
      bits == 2 || bits == 3 || bits == 4 || bits == 5 || bits == 6 ||
          bits == 8,
      "Template undefined for bits not in {2, 3, 4, 5, 6, 8}");

  U sum = 0;

  if (bits == 2) {
    for (int i = 0; i < N; i += 4) {
      sum += x[i] + x[i + 1] + x[i + 2] + x[i + 3];
      x_thread[i] = x[i];
      x_thread[i + 1] = x[i + 1] / 4.0f;
      x_thread[i + 2] = x[i + 2] / 16.0f;
      x_thread[i + 3] = x[i + 3] / 64.0f;
    }
  }

  else if (bits == 3) {
    for (int i = 0; i < N; i += 8) {
      sum += x[i] + x[i + 1] + x[i + 2] + x[i + 3] + x[i + 4] + x[i + 5] +
          x[i + 6] + x[i + 7];

      x_thread[i] = x[i];
      x_thread[i + 1] = x[i + 1] / 8.0f;
      x_thread[i + 2] = x[i + 2] / 64.0f;
      x_thread[i + 3] = x[i + 3] / 2.0f;
      x_thread[i + 4] = x[i + 4] / 16.0f;
      x_thread[i + 5] = x[i + 5] / 128.0f;
      x_thread[i + 6] = x[i + 6] / 4.0f;
      x_thread[i + 7] = x[i + 7] / 32.0f;
    }
  }

  else if (bits == 4) {
    for (int i = 0; i < N; i += 4) {
      sum += x[i] + x[i + 1] + x[i + 2] + x[i + 3];
      x_thread[i] = x[i];
      x_thread[i + 1] = x[i + 1] / 16.0f;
      x_thread[i + 2] = x[i + 2] / 256.0f;
      x_thread[i + 3] = x[i + 3] / 4096.0f;
    }
  }

  else if (bits == 5) {
    for (int i = 0; i < N; i += 8) {
      sum += x[i] + x[i + 1] + x[i + 2] + x[i + 3] + x[i + 4] + x[i + 5] +
          x[i + 6] + x[i + 7];
      x_thread[i] = x[i];
      x_thread[i + 1] = x[i + 1] / 32.0f;
      x_thread[i + 2] = x[i + 2] / 4.0f;
      x_thread[i + 3] = x[i + 3] / 128.0f;
      x_thread[i + 4] = x[i + 4] / 16.0f;
      x_thread[i + 5] = x[i + 5] / 2.0f;
      x_thread[i + 6] = x[i + 6] / 64.0f;
      x_thread[i + 7] = x[i + 7] / 8.0f;
    }
  }

  else if (bits == 6) {
    for (int i = 0; i < N; i += 4) {
      sum += x[i] + x[i + 1] + x[i + 2] + x[i + 3];
      x_thread[i] = x[i];
      x_thread[i + 1] = x[i + 1] / 64.0f;
      x_thread[i + 2] = x[i + 2] / 16.0f;
      x_thread[i + 3] = x[i + 3] / 4.0f;
    }
  }

  else if (bits == 8) {
    for (int i = 0; i < N; i++) {
      sum += x[i];
      x_thread[i] = x[i];
    }
  }

  for (int i = N; i < values_per_thread; i++) {
    x_thread[i] = 0;
  }

  return sum;
}

// ─────────────────────────────────────────────────────────────────
// qdot / qdot_safe — quantized.h:191-392
// ─────────────────────────────────────────────────────────────────

template <typename U, int values_per_thread, int bits>
inline U qdot(
    const device uint8_t* w,
    const thread U* x_thread,
    U scale,
    U bias,
    U sum) {
  static_assert(
      bits == 2 || bits == 3 || bits == 4 || bits == 5 || bits == 6 ||
          bits == 8,
      "Template undefined for bits not in {2, 3, 4, 5, 6, 8}");

  U accum = 0;

  if (bits == 2) {
    for (int i = 0; i < (values_per_thread / 4); i++) {
      accum +=
          (x_thread[4 * i] * (w[i] & 0x03) +
           x_thread[4 * i + 1] * (w[i] & 0x0c) +
           x_thread[4 * i + 2] * (w[i] & 0x30) +
           x_thread[4 * i + 3] * (w[i] & 0xc0));
    }
  }

  else if (bits == 3) {
    for (int i = 0; i < (values_per_thread / 8); i++) {
      x_thread += 8 * i;
      w += 3 * i;

      accum += (w[0] & 0x07) * x_thread[0];
      accum += (w[0] & 0x38) * x_thread[1];
      accum += (w[0] & 0xc0) * x_thread[2];
      accum += (w[1] & 0x01) * (x_thread[2] * 256.0f);

      accum += (w[1] & 0x0e) * x_thread[3];
      accum += (w[1] & 0x70) * x_thread[4];
      accum += (w[1] & 0x80) * x_thread[5];
      accum += (w[2] & 0x03) * (x_thread[5] * 256.0f);

      accum += (w[2] & 0x1c) * x_thread[6];
      accum += (w[2] & 0xe0) * x_thread[7];
    }
  }

  else if (bits == 4) {
    const device uint16_t* ws = (const device uint16_t*)w;
    for (int i = 0; i < (values_per_thread / 4); i++) {
      const uint16_t wi = ws[i] ^ AFFINE_CODES_XOR;
      accum +=
          (x_thread[4 * i] * (wi & 0x000f) +
           x_thread[4 * i + 1] * (wi & 0x00f0) +
           x_thread[4 * i + 2] * (wi & 0x0f00) +
           x_thread[4 * i + 3] * (wi & 0xf000));
    }
  }

  else if (bits == 5) {
    for (int i = 0; i < (values_per_thread / 8); i++) {
      x_thread += 8 * i;
      w += 5 * i;

      accum += (w[0] & 0x1f) * x_thread[0];
      accum += (w[0] & 0xe0) * x_thread[1];
      accum += (w[1] & 0x3) * (x_thread[1] * 256.0f);
      accum += (w[1] & 0x7c) * x_thread[2];
      accum += (w[1] & 0x80) * x_thread[3];
      accum += (w[2] & 0xf) * (x_thread[3] * 256.0f);
      accum += (w[2] & 0xf0) * x_thread[4];
      accum += (w[3] & 0x1) * (x_thread[4] * 256.0f);
      accum += (w[3] & 0x3e) * x_thread[5];
      accum += (w[3] & 0xc0) * x_thread[6];
      accum += (w[4] & 0x7) * (x_thread[6] * 256.0f);
      accum += (w[4] & 0xf8) * x_thread[7];
    }
  }

  else if (bits == 6) {
    for (int i = 0; i < (values_per_thread / 4); i++) {
      x_thread += 4 * i;
      w += 3 * i;

      accum += (w[0] & 0x3f) * x_thread[0];

      accum += (w[0] & 0xc0) * x_thread[1];
      accum += (w[1] & 0x0f) * (x_thread[1] * 256.0f);

      accum += (w[1] & 0xf0) * x_thread[2];
      accum += (w[2] & 0x03) * (x_thread[2] * 256.0f);

      accum += (w[2] & 0xfc) * x_thread[3];
    }
  }

  else if (bits == 8) {
    for (int i = 0; i < values_per_thread; i++) {
      accum += x_thread[i] * w[i];
    }
  }

  return scale * accum + sum * bias;
}

template <typename U, int values_per_thread, int bits>
inline U qdot_safe(
    const device uint8_t* w,
    const thread U* x_thread,
    U scale,
    U bias,
    U sum,
    int N) {
  static_assert(
      bits == 2 || bits == 3 || bits == 4 || bits == 5 || bits == 6 ||
          bits == 8,
      "Template undefined for bits not in {2, 3, 4, 5, 6, 8}");

  U accum = 0;

  if (bits == 2) {
    for (int i = 0; i < (N / 4); i++) {
      accum +=
          (x_thread[4 * i] * (w[i] & 0x03) +
           x_thread[4 * i + 1] * (w[i] & 0x0c) +
           x_thread[4 * i + 2] * (w[i] & 0x30) +
           x_thread[4 * i + 3] * (w[i] & 0xc0));
    }
  }

  else if (bits == 3) {
    for (int i = 0; i < (N / 8); i++) {
      x_thread += 8 * i;
      w += 3 * i;

      accum += (w[0] & 0x07) * x_thread[0];
      accum += (w[0] & 0x38) * x_thread[1];
      accum += (w[0] & 0xc0) * x_thread[2];
      accum += (w[1] & 0x01) * (x_thread[2] * 256.0f);

      accum += (w[1] & 0x0e) * x_thread[3];
      accum += (w[1] & 0x70) * x_thread[4];
      accum += (w[1] & 0x80) * x_thread[5];
      accum += (w[2] & 0x03) * (x_thread[5] * 256.0f);

      accum += (w[2] & 0x1c) * x_thread[6];
      accum += (w[2] & 0xe0) * x_thread[7];
    }
  }

  else if (bits == 4) {
    const device uint16_t* ws = (const device uint16_t*)w;
    for (int i = 0; i < (N / 4); i++) {
      const uint16_t wi = ws[i] ^ AFFINE_CODES_XOR;
      accum +=
          (x_thread[4 * i] * (wi & 0x000f) +
           x_thread[4 * i + 1] * (wi & 0x00f0) +
           x_thread[4 * i + 2] * (wi & 0x0f00) +
           x_thread[4 * i + 3] * (wi & 0xf000));
    }
  }

  else if (bits == 5) {
    for (int i = 0; i < (N / 8); i++) {
      x_thread += 8 * i;
      w += 5 * i;

      accum += (w[0] & 0x1f) * x_thread[0];
      accum += (w[0] & 0xe0) * x_thread[1];
      accum += (w[1] & 0x3) * (x_thread[1] * 256.0f);
      accum += (w[1] & 0x7c) * x_thread[2];
      accum += (w[1] & 0x80) * x_thread[3];
      accum += (w[2] & 0xf) * (x_thread[3] * 256.0f);
      accum += (w[2] & 0xf0) * x_thread[4];
      accum += (w[3] & 0x1) * (x_thread[4] * 256.0f);
      accum += (w[3] & 0x3e) * x_thread[5];
      accum += (w[3] & 0xc0) * x_thread[6];
      accum += (w[4] & 0x7) * (x_thread[6] * 256.0f);
      accum += (w[4] & 0xf8) * x_thread[7];
    }
  }

  else if (bits == 6) {
    for (int i = 0; i < (N / 4); i++) {
      x_thread += 4 * i;
      w += 3 * i;

      accum += (w[0] & 0x3f) * x_thread[0];

      accum += (w[0] & 0xc0) * x_thread[1];
      accum += (w[1] & 0x0f) * (x_thread[1] * 256.0f);

      accum += (w[1] & 0xf0) * x_thread[2];
      accum += (w[2] & 0x03) * (x_thread[2] * 256.0f);

      accum += (w[2] & 0xfc) * x_thread[3];
    }
  }

  else if (bits == 8) {
    for (int i = 0; i < N; i++) {
      accum += x_thread[i] * w[i];
    }
  }

  return scale * accum + sum * bias;
}

// ─────────────────────────────────────────────────────────────────
// elem_to_loc helpers — utils.h:97-125 + steel/utils.h:7-42
// ─────────────────────────────────────────────────────────────────

template <typename IdxT = int64_t>
METAL_FUNC IdxT elem_to_loc(
    uint elem,
    constant const int* shape,
    constant const int64_t* strides,
    int ndim) {
  IdxT loc = 0;
  for (int i = ndim - 1; i >= 0 && elem > 0; --i) {
    loc += (elem % shape[i]) * IdxT(strides[i]);
    elem /= shape[i];
  }
  return loc;
}

METAL_FUNC ulong3 elem_to_loc_broadcast(
    uint elem,
    constant const int* shape,
    constant const int64_t* a_strides,
    constant const int64_t* b_strides,
    constant const int64_t* c_strides,
    int ndim) {
  ulong loc_a{0};
  ulong loc_b{0};
  ulong loc_c{0};
  for (int i = ndim - 1; i >= 0 && elem > 0; --i) {
    int pos_in_dim = (elem % shape[i]);
    elem /= shape[i];
    loc_a += pos_in_dim * a_strides[i];
    loc_b += pos_in_dim * b_strides[i];
    loc_c += pos_in_dim * c_strides[i];
  }
  return ulong3(loc_a, loc_b, loc_c);
}

// ─────────────────────────────────────────────────────────────────
// adjust_matrix_offsets — quantized.h:1351-1387 (single-array form)
// ─────────────────────────────────────────────────────────────────

template <typename T_act, typename T_scale>
METAL_FUNC void adjust_matrix_offsets(
    const device T_act*& x,
    const device uint32_t*& w,
    const device T_scale*& scales,
    const device T_scale*& biases,
    device T_act*& y,
    int output_stride,
    const constant int& x_batch_ndims,
    const constant int* x_shape,
    const constant int64_t* x_strides,
    const constant int& w_batch_ndims,
    const constant int* w_shape,
    const constant int64_t* w_strides,
    const constant int64_t* s_strides,
    const constant int64_t* b_strides,
    uint3 tid [[threadgroup_position_in_grid]]) {
  uint32_t x_idx = tid.z;
  uint32_t w_idx = tid.z;
  if (x_batch_ndims == 1) {
    x += x_idx * x_strides[0];
  } else {
    x += elem_to_loc(x_idx, x_shape, x_strides, x_batch_ndims);
  }
  if (w_batch_ndims == 1) {
    w += w_idx * w_strides[0];
    scales += w_idx * s_strides[0];
    biases += w_idx * b_strides[0];
  } else {
    ulong3 idx = elem_to_loc_broadcast(
        w_idx, w_shape, w_strides, s_strides, b_strides, w_batch_ndims);
    w += idx.x;
    scales += idx.y;
    biases += idx.z;
  }
  y += tid.z * output_stride;
}

// ─────────────────────────────────────────────────────────────────
// qmv_quad_impl — quantized.h:692-747
// ─────────────────────────────────────────────────────────────────

template <typename T_act, typename T_scale, int group_size, int bits, int D>
METAL_FUNC void qmv_quad_impl(
    const device uint32_t* w,
    const device T_scale* scales,
    const device T_scale* biases,
    const device T_act* x,
    device T_act* y,
    // K / N are compiled-in constants on the kernel side
    // (IN_VEC_SIZE / OUT_VEC_SIZE) and forwarded by-value here so
    // the impl body matches the MLX C++ source line-for-line.
    int in_vec_size,
    int out_vec_size,
    uint3 tid [[threadgroup_position_in_grid]],
    uint quad_gid [[quadgroup_index_in_threadgroup]],
    uint quad_lid [[thread_index_in_quadgroup]],
    const device T_scale* gain = nullptr,
    const device T_act* bias = nullptr) {
  constexpr int quads_per_simd = SIMD_SIZE / QUAD_SIZE;
  constexpr int pack_factor = 32 / bits;
  constexpr int values_per_thread = D / QUAD_SIZE;
  constexpr int packs_per_thread = values_per_thread / pack_factor;
  constexpr int scale_step_per_thread = group_size / values_per_thread;
  constexpr int results_per_quadgroup = 8;

  typedef float U;

  thread U x_thread[values_per_thread];
  thread U result[results_per_quadgroup] = {0};

  // Adjust positions
  const int in_vec_size_w = in_vec_size / pack_factor;
  const int in_vec_size_g = in_vec_size / group_size;
  const int out_row = tid.y * quads_per_simd * results_per_quadgroup + quad_gid;

  w += out_row * in_vec_size_w + quad_lid * packs_per_thread;
  scales += out_row * in_vec_size_g + quad_lid / scale_step_per_thread;
  biases += out_row * in_vec_size_g + quad_lid / scale_step_per_thread;
  x += tid.x * in_vec_size + quad_lid * values_per_thread;
  y += tid.x * out_vec_size + out_row;

  U sum;
  U sum_sq = 0;
  if (QMV_NORMED) {
    thread U xg[values_per_thread];
    qmv_normalize<U, values_per_thread>(
        x, gain + quad_lid * values_per_thread, xg, sum_sq, values_per_thread);
    sum = load_vector<T_act, U, values_per_thread, bits>(xg, x_thread);
  } else {
    sum = load_vector<T_act, U, values_per_thread, bits>(x, x_thread);
  }

  for (int row = 0; row < results_per_quadgroup; row++) {
    auto wl = (const device uint8_t*)(w + row * in_vec_size_w * quads_per_simd);
    const device T_scale* sl = scales + row * in_vec_size_g * quads_per_simd;
    const device T_scale* bl = biases + row * in_vec_size_g * quads_per_simd;

    // T_scale → U=float expands at load (MLX qmv pattern, quantized.h:692-816).
    U s = sl[0];
    U b = bl[0];
    if (row * quads_per_simd + out_row < out_vec_size) {
      result[row] += qdot<U, values_per_thread, bits>(wl, x_thread, s, b, sum);
    }
  }

  const U scale = qmv_row_scale(QMV_NORMED ? quad_sum(sum_sq) : 0, in_vec_size);
  for (int row = 0; row < results_per_quadgroup; row++) {
    result[row] = quad_sum(result[row]) * scale;
    if (quad_lid == 0 && row * quads_per_simd + out_row < out_vec_size) {
      qmv_store<T_act>(y + row * quads_per_simd, result[row], bias, out_row + row * quads_per_simd);
    }
  }
}

// ─────────────────────────────────────────────────────────────────
// qmv_fast_impl — quantized.h:749-814
// ─────────────────────────────────────────────────────────────────

template <typename T_act, typename T_scale, int group_size, int bits,
          typename Y = device T_act*>
METAL_FUNC void qmv_fast_impl(
    const device uint32_t* w,
    const device T_scale* scales,
    const device T_scale* biases,
    const device T_act* x,
    Y y,
    int in_vec_size,
    int out_vec_size,
    uint3 tid [[threadgroup_position_in_grid]],
    uint simd_gid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]],
    const device T_scale* gain = nullptr,
    const device T_act* bias = nullptr) {
  constexpr int packs_per_thread = bits == 2 ? 1 : 2;
  constexpr int num_simdgroups = 2;
  constexpr int results_per_simdgroup = 4;
  constexpr int pack_factor = get_pack_factor<bits, 32>();
  constexpr int bytes_per_pack = get_bytes_per_pack<bits, 32>();
  constexpr int values_per_thread = pack_factor * packs_per_thread;
  constexpr int block_size = values_per_thread * SIMD_SIZE;
  constexpr int scale_step_per_thread = group_size / values_per_thread;

  const device uint8_t* ws = (const device uint8_t*)w;

  typedef float U;

  thread U x_thread[values_per_thread];
  thread U result[results_per_simdgroup] = {0};

  // Adjust positions
  const int in_vec_size_w = in_vec_size * bytes_per_pack / pack_factor;
  const int in_vec_size_g = in_vec_size / group_size;
  const int out_row = tid.y * (num_simdgroups * results_per_simdgroup) +
      simd_gid * results_per_simdgroup;

  ws += out_row * in_vec_size_w + simd_lid * packs_per_thread * bytes_per_pack;
  scales += out_row * in_vec_size_g + simd_lid / scale_step_per_thread;
  biases += out_row * in_vec_size_g + simd_lid / scale_step_per_thread;
  x += tid.x * in_vec_size + simd_lid * values_per_thread;
  y += tid.x * out_vec_size + out_row;
  gain += simd_lid * values_per_thread;
  U sum_sq = 0;

  // Whole blocks, then the rest of the row: K % block_size, a multiple of values_per_thread
  // (`qmv_fast_covers`), each lane below it taking one more whole chunk — the full-width loads,
  // where `qmv_impl` takes half a chunk per lane over the whole row.
  const int whole = in_vec_size - in_vec_size % block_size;
  for (int k = 0; k < in_vec_size; k += block_size) {
    if (k == whole && int(simd_lid) * values_per_thread >= in_vec_size - whole) {
      break;
    }
    U sum;
    if (QMV_NORMED) {
      thread U xg[values_per_thread];
      qmv_normalize<U, values_per_thread>(x, gain, xg, sum_sq, values_per_thread);
      sum = load_vector<T_act, U, values_per_thread, bits>(xg, x_thread);
      gain += block_size;
    } else {
      sum = load_vector<T_act, U, values_per_thread, bits>(x, x_thread);
    }

    for (int row = 0; row < results_per_simdgroup; row++) {
      auto wl = (const device uint8_t*)(ws + row * in_vec_size_w);
      const device T_scale* sl = scales + row * in_vec_size_g;
      const device T_scale* bl = biases + row * in_vec_size_g;

      U s = sl[0];
      U b = bl[0];
      result[row] += qdot<U, values_per_thread, bits>(wl, x_thread, s, b, sum);
    }

    ws += block_size * bytes_per_pack / pack_factor;
    scales += block_size / group_size;
    biases += block_size / group_size;
    x += block_size;
  }

  const U scale = qmv_row_scale(QMV_NORMED ? simd_sum(sum_sq) : 0, in_vec_size);
  for (int row = 0; row < results_per_simdgroup; row++) {
    result[row] = simd_sum(result[row]) * scale;
    if (simd_lid == 0) {
      qmv_store<T_act>(y + row, result[row], bias, out_row + row);
    }
  }
}

// ─────────────────────────────────────────────────────────────────
// qmv_impl — quantized.h:816-975
// ─────────────────────────────────────────────────────────────────

template <typename T_act, typename T_scale, int group_size, int bits,
          typename Y = device T_act*>
METAL_FUNC void qmv_impl(
    const device uint32_t* w,
    const device T_scale* scales,
    const device T_scale* biases,
    const device T_act* x,
    Y y,
    int in_vec_size,
    int out_vec_size,
    uint3 tid [[threadgroup_position_in_grid]],
    uint simd_gid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]],
    const device T_scale* gain = nullptr,
    const device T_act* bias = nullptr) {
  constexpr int num_simdgroups = 2;
  constexpr int results_per_simdgroup = 4;
  constexpr int packs_per_thread = 1;
  constexpr int pack_factor = get_pack_factor<bits, 32>();
  constexpr int bytes_per_pack = get_bytes_per_pack<bits, 32>();

  constexpr int values_per_thread = pack_factor * packs_per_thread;
  constexpr int block_size = values_per_thread * SIMD_SIZE;
  constexpr int scale_step_per_thread = group_size / values_per_thread;

  const device uint8_t* ws = (const device uint8_t*)w;

  typedef float U;

  thread U x_thread[values_per_thread];
  thread U result[results_per_simdgroup] = {0};

  // Adjust positions
  const int in_vec_size_w = in_vec_size * bytes_per_pack / pack_factor;
  const int in_vec_size_g = in_vec_size / group_size;
  const int out_row = tid.y * (num_simdgroups * results_per_simdgroup) +
      simd_gid * results_per_simdgroup;
  const int used_out_row = min(out_vec_size - results_per_simdgroup, out_row);

  if (out_row >= out_vec_size) {
    return;
  }
  gain += simd_lid * values_per_thread;
  U sum_sq = 0;

  // In this case we need to properly guard all our reads because there isn't
  // even 1 tile in the matrix
  if (out_vec_size < (num_simdgroups * results_per_simdgroup)) {
    ws +=
        out_row * in_vec_size_w + simd_lid * packs_per_thread * bytes_per_pack;
    scales += out_row * in_vec_size_g + simd_lid / scale_step_per_thread;
    biases += out_row * in_vec_size_g + simd_lid / scale_step_per_thread;
    x += tid.x * in_vec_size + simd_lid * values_per_thread;
    y += tid.x * out_vec_size + out_row;

    int k = 0;
    for (; k < in_vec_size - block_size; k += block_size) {
      U sum;
      if (QMV_NORMED) {
        thread U xg[values_per_thread];
        qmv_normalize<U, values_per_thread>(x, gain, xg, sum_sq, values_per_thread);
        sum = load_vector<T_act, U, values_per_thread, bits>(xg, x_thread);
        gain += block_size;
      } else {
        sum = load_vector<T_act, U, values_per_thread, bits>(x, x_thread);
      }

      for (int row = 0;
           row < results_per_simdgroup && out_row + row < out_vec_size;
           row++) {
        auto wl = (const device uint8_t*)(ws + row * in_vec_size_w);
        const device T_scale* sl = scales + row * in_vec_size_g;
        const device T_scale* bl = biases + row * in_vec_size_g;

        U s = sl[0];
        U b = bl[0];
        result[row] +=
            qdot<U, values_per_thread, bits>(wl, x_thread, s, b, sum);
      }

      ws += block_size * bytes_per_pack / pack_factor;
      scales += block_size / group_size;
      biases += block_size / group_size;
      x += block_size;
    }
    const int remaining = clamp(
        static_cast<int>(in_vec_size - k - simd_lid * values_per_thread),
        0,
        values_per_thread);
    if (remaining > 0) {
      U sum;
      if (QMV_NORMED) {
        thread U xg[values_per_thread];
        qmv_normalize<U, values_per_thread>(x, gain, xg, sum_sq, remaining);
        sum = load_vector_safe<T_act, U, values_per_thread, bits>(xg, x_thread, remaining);
      } else {
        sum = load_vector_safe<T_act, U, values_per_thread, bits>(x, x_thread, remaining);
      }

      for (int row = 0;
           row < results_per_simdgroup && out_row + row < out_vec_size;
           row++) {
        auto wl = (const device uint8_t*)(ws + row * in_vec_size_w);
        const device T_scale* sl = scales + row * in_vec_size_g;
        const device T_scale* bl = biases + row * in_vec_size_g;

        U s = sl[0];
        U b = bl[0];
        result[row] += qdot_safe<U, values_per_thread, bits>(
            wl, x_thread, s, b, sum, remaining);
      }
    }

    const U scale = qmv_row_scale(QMV_NORMED ? simd_sum(sum_sq) : 0, in_vec_size);
    for (int row = 0;
         row < results_per_simdgroup && out_row + row < out_vec_size;
         row++) {
      result[row] = simd_sum(result[row]) * scale;
      if (simd_lid == 0) {
        qmv_store<T_act>(y + row, result[row], bias, out_row + row);
      }
    }
  }

  // In this case the last tile is moved back to redo some output values
  else {
    ws += used_out_row * in_vec_size_w +
        simd_lid * packs_per_thread * bytes_per_pack;
    scales += used_out_row * in_vec_size_g + simd_lid / scale_step_per_thread;
    biases += used_out_row * in_vec_size_g + simd_lid / scale_step_per_thread;
    x += tid.x * in_vec_size + simd_lid * values_per_thread;
    y += tid.x * out_vec_size + used_out_row;

    int k = 0;
    for (; k < in_vec_size - block_size; k += block_size) {
      U sum;
      if (QMV_NORMED) {
        thread U xg[values_per_thread];
        qmv_normalize<U, values_per_thread>(x, gain, xg, sum_sq, values_per_thread);
        sum = load_vector<T_act, U, values_per_thread, bits>(xg, x_thread);
        gain += block_size;
      } else {
        sum = load_vector<T_act, U, values_per_thread, bits>(x, x_thread);
      }

      for (int row = 0; row < results_per_simdgroup; row++) {
        auto wl = (const device uint8_t*)(ws + row * in_vec_size_w);
        const device T_scale* sl = scales + row * in_vec_size_g;
        const device T_scale* bl = biases + row * in_vec_size_g;

        U s = sl[0];
        U b = bl[0];
        result[row] +=
            qdot<U, values_per_thread, bits>(wl, x_thread, s, b, sum);
      }

      ws += block_size * bytes_per_pack / pack_factor;
      scales += block_size / group_size;
      biases += block_size / group_size;
      x += block_size;
    }
    const int remaining = clamp(
        static_cast<int>(in_vec_size - k - simd_lid * values_per_thread),
        0,
        values_per_thread);
    if (remaining > 0) {
      U sum;
      if (QMV_NORMED) {
        thread U xg[values_per_thread];
        qmv_normalize<U, values_per_thread>(x, gain, xg, sum_sq, remaining);
        sum = load_vector_safe<T_act, U, values_per_thread, bits>(xg, x_thread, remaining);
      } else {
        sum = load_vector_safe<T_act, U, values_per_thread, bits>(x, x_thread, remaining);
      }

      for (int row = 0; row < results_per_simdgroup; row++) {
        auto wl = (const device uint8_t*)(ws + row * in_vec_size_w);
        const device T_scale* sl = scales + row * in_vec_size_g;
        const device T_scale* bl = biases + row * in_vec_size_g;

        U s = sl[0];
        U b = bl[0];
        result[row] += qdot_safe<U, values_per_thread, bits>(
            wl, x_thread, s, b, sum, remaining);
      }
    }
    const U scale = qmv_row_scale(QMV_NORMED ? simd_sum(sum_sq) : 0, in_vec_size);
    for (int row = 0; row < results_per_simdgroup; row++) {
      result[row] = simd_sum(result[row]) * scale;
      if (simd_lid == 0 && (!QMV_ADDS || used_out_row + row >= out_row)) {
        qmv_store<T_act>(y + row, result[row], bias, used_out_row + row);
      }
    }
  }
}

// ─────────────────────────────────────────────────────────────────
// affine_qmv_quad — quantized.h:1443-1493
// ─────────────────────────────────────────────────────────────────

template <typename T_act, typename T_scale, int group_size, int bits, int D, bool batched>
[[kernel]] void affine_qmv_quad(
    const device uint32_t* w [[buffer(0)]],
    const device T_scale* scales [[buffer(1)]],
    const device T_scale* biases [[buffer(2)]],
    const device T_act* x [[buffer(3)]],
    device T_act* y [[buffer(4)]],
    // buffer(5) / buffer(6) (in_vec_size / out_vec_size) replaced by
    // file-scope constants IN_VEC_SIZE / OUT_VEC_SIZE so this
    // kernel is recordable into an MTLIndirectComputeCommand (which
    // exposes setKernelBuffer but not setKernelBytes).
    const constant int& x_batch_ndims [[buffer(7)]],
    const constant int* x_shape [[buffer(8)]],
    const constant int64_t* x_strides [[buffer(9)]],
    const constant int& w_batch_ndims [[buffer(10)]],
    const constant int* w_shape [[buffer(11)]],
    const constant int64_t* w_strides [[buffer(12)]],
    const constant int64_t* s_strides [[buffer(13)]],
    const constant int64_t* b_strides [[buffer(14)]],
    const device T_scale* gain [[buffer(15)]],
    const device T_act* bias [[buffer(16)]],
    uint3 tid [[threadgroup_position_in_grid]],
    uint quad_gid [[quadgroup_index_in_threadgroup]],
    uint quad_lid [[thread_index_in_quadgroup]]) {
  if (batched) {
    int M = x_shape[x_batch_ndims];
    adjust_matrix_offsets<T_act, T_scale>(
        x,
        w,
        scales,
        biases,
        y,
        OUT_VEC_SIZE * M,
        x_batch_ndims,
        x_shape,
        x_strides,
        w_batch_ndims,
        w_shape,
        w_strides,
        s_strides,
        b_strides,
        tid);
  }
  qmv_quad_impl<T_act, T_scale, group_size, bits, D>(
      w,
      scales,
      biases,
      x,
      y,
      IN_VEC_SIZE,
      OUT_VEC_SIZE,
      tid,
      quad_gid,
      quad_lid,
      gain,
      bias);
}

// ─────────────────────────────────────────────────────────────────
// affine_qmv_fast — quantized.h:1495-1545
// ─────────────────────────────────────────────────────────────────

template <typename T_act, typename T_scale, int group_size, int bits, bool batched>
[[kernel]] void affine_qmv_fast(
    const device uint32_t* w [[buffer(0)]],
    const device T_scale* scales [[buffer(1)]],
    const device T_scale* biases [[buffer(2)]],
    const device T_act* x [[buffer(3)]],
    device T_act* y [[buffer(4)]],
    // buffer(5) / buffer(6): see note on affine_qmv_quad above —
    // K / N are the compiled-in IN_VEC_SIZE / OUT_VEC_SIZE.
    const constant int& x_batch_ndims [[buffer(7)]],
    const constant int* x_shape [[buffer(8)]],
    const constant int64_t* x_strides [[buffer(9)]],
    const constant int& w_batch_ndims [[buffer(10)]],
    const constant int* w_shape [[buffer(11)]],
    const constant int64_t* w_strides [[buffer(12)]],
    const constant int64_t* s_strides [[buffer(13)]],
    const constant int64_t* b_strides [[buffer(14)]],
    const device T_scale* gain [[buffer(15)]],
    const device T_act* bias [[buffer(16)]],
    uint3 tid [[threadgroup_position_in_grid]],
    uint simd_gid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]]) {
  if (batched) {
    int M = x_shape[x_batch_ndims];
    adjust_matrix_offsets<T_act, T_scale>(
        x,
        w,
        scales,
        biases,
        y,
        OUT_VEC_SIZE * M,
        x_batch_ndims,
        x_shape,
        x_strides,
        w_batch_ndims,
        w_shape,
        w_strides,
        s_strides,
        b_strides,
        tid);
  }
  qmv_fast_impl<T_act, T_scale, group_size, bits>(
      w,
      scales,
      biases,
      x,
      y,
      IN_VEC_SIZE,
      OUT_VEC_SIZE,
      tid,
      simd_gid,
      simd_lid,
      gain,
      bias);
}

// ─────────────────────────────────────────────────────────────────
// affine_qmv — quantized.h:1547-1597
// ─────────────────────────────────────────────────────────────────

template <typename T_act, typename T_scale, const int group_size, const int bits, bool batched>
[[kernel]] void affine_qmv(
    const device uint32_t* w [[buffer(0)]],
    const device T_scale* scales [[buffer(1)]],
    const device T_scale* biases [[buffer(2)]],
    const device T_act* x [[buffer(3)]],
    device T_act* y [[buffer(4)]],
    // buffer(5) / buffer(6): see note on affine_qmv_quad above —
    // K / N are the compiled-in IN_VEC_SIZE / OUT_VEC_SIZE.
    const constant int& x_batch_ndims [[buffer(7)]],
    const constant int* x_shape [[buffer(8)]],
    const constant int64_t* x_strides [[buffer(9)]],
    const constant int& w_batch_ndims [[buffer(10)]],
    const constant int* w_shape [[buffer(11)]],
    const constant int64_t* w_strides [[buffer(12)]],
    const constant int64_t* s_strides [[buffer(13)]],
    const constant int64_t* b_strides [[buffer(14)]],
    const device T_scale* gain [[buffer(15)]],
    const device T_act* bias [[buffer(16)]],
    uint3 tid [[threadgroup_position_in_grid]],
    uint simd_gid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]]) {
  if (batched) {
    int M = x_shape[x_batch_ndims];
    adjust_matrix_offsets<T_act, T_scale>(
        x,
        w,
        scales,
        biases,
        y,
        OUT_VEC_SIZE * M,
        x_batch_ndims,
        x_shape,
        x_strides,
        w_batch_ndims,
        w_shape,
        w_strides,
        s_strides,
        b_strides,
        tid);
  }
  qmv_impl<T_act, T_scale, group_size, bits>(
      w,
      scales,
      biases,
      x,
      y,
      IN_VEC_SIZE,
      OUT_VEC_SIZE,
      tid,
      simd_gid,
      simd_lid,
      gain,
      bias);
}

// ─────────────────────────────────────────────────────────────────
// dequantize — quantized.h:482-556. Decode one quantized block
// (scale * q + bias) into w_local. Bits 4 and 8 only (the wide
// kernel's instantiations); the other branches dropped rather than
// kept dead — this copy exists solely for qmv_wide_impl.
// ─────────────────────────────────────────────────────────────────

template <typename U, int N, int bits, typename W>
inline void dequantize(const device uint8_t* w, U scale, U bias, W w_local) {
  static_assert(
      bits == 4 || bits == 8,
      "dequantize: scratchy instantiates bits 4 and 8 only");

  const float s = float(scale);
  const float b = float(bias);

  if (bits == 4) {
    // Codes as stored are UNSIGNED in MLX; our storage may hold them
    // XOR 0x88 (signed q - 8, `AffineCodes::Offset8`, baked constant
    // 5) — the same `AFFINE_CODES_XOR` every other kernel in this file
    // applies. Un-XOR the byte before splitting its nibbles.
    const uint8_t xor8 = AFFINE_CODES_XOR ? 0x88 : 0;
    float sc[2] = {s, s / 16.0f};
    for (int i = 0; i < (N / 2); i++) {
      const uint8_t wb = w[i] ^ xor8;
      w_local[2 * i] = static_cast<U>(sc[0] * (wb & 0x0f) + b);
      w_local[2 * i + 1] = static_cast<U>(sc[1] * (wb & 0xf0) + b);
    }
  }

  else if (bits == 8) {
    for (int i = 0; i < N; i++) {
      w_local[i] = static_cast<U>(s * w[i] + b);
    }
  }
}

// ─────────────────────────────────────────────────────────────────
// qmv_wide_impl — quantized.h:984-1075. The small-M band kernel
// (2 ≤ M < vector_limit): each weight group is dequantized ONCE and
// reused across the `vecs_per_tg` input vectors, so the weight
// traffic is M-independent where the plain qmv re-streams the whole
// matrix per row. `k_lanes` lanes reduce K per output row;
// 32/k_lanes rows per simdgroup; the partials fold with a shuffle
// ladder (simd_sum would mix the rows a simdgroup spans). Its ends as
// `qmv_fast_impl`'s: each vector normalized as it loads (its sum of
// squares folded with its dot) and its rows stored through `qmv_store`.
// ─────────────────────────────────────────────────────────────────

template <typename T_act, typename T_scale, int group_size, int bits, int vecs_per_tg, int k_lanes>
METAL_FUNC void qmv_wide_impl(
    const device uint32_t* w,
    const device T_scale* scales,
    const device T_scale* biases,
    const device T_act* x,
    device T_act* y,
    int M,
    uint3 tid [[threadgroup_position_in_grid]],
    uint simd_gid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]],
    const device T_scale* gain,
    const device T_act* bias) {
  constexpr int num_simdgroups = 2;
  constexpr int results_per_simdgroup = SIMD_SIZE / k_lanes;
  constexpr int sub = 8; // values per sub-chunk (== bits bytes, byte-aligned)

  typedef float U;

  const short k_lane = simd_lid % k_lanes;
  const short sg_row = simd_lid / k_lanes;

  const int out_row = tid.y * (results_per_simdgroup * num_simdgroups) +
      results_per_simdgroup * simd_gid + sg_row;
  const int vec0 = tid.x * vecs_per_tg;

  const int row = min(out_row, OUT_VEC_SIZE - 1);

  const int in_vec_size_w = IN_VEC_SIZE * bits / 8; // bytes per weight row
  const int in_vec_size_g = IN_VEC_SIZE / group_size;
  const device uint8_t* wrow = (const device uint8_t*)w + row * in_vec_size_w;
  const device T_scale* srow = scales + row * in_vec_size_g;
  const device T_scale* brow = biases + row * in_vec_size_g;

  // One device pointer per streamed vector; the clamp keeps an out-of-range
  // tail slot reading a valid row (it is never written below).
  const device T_act* xv[vecs_per_tg];
  for (int v = 0; v < vecs_per_tg; v++) {
    xv[v] = x + min(vec0 + v, M - 1) * IN_VEC_SIZE;
  }

  U result[vecs_per_tg] = {0};
  U sum_sq[vecs_per_tg] = {0};

  // Each lane reduces a strided subset of the row's groups: decode the group
  // in 8-value sub-chunks and reuse each chunk across the streamed vectors.
  for (int g = k_lane; g < in_vec_size_g; g += k_lanes) {
    U scale = srow[g];
    U bias = brow[g];
#pragma unroll
    for (int sc = 0; sc < group_size / sub; sc++) {
      const int k0 = g * group_size + sc * sub;
      const device uint8_t* wc = wrow + k0 * bits / 8;
      U w_dq[sub];
      dequantize<U, sub, bits>(wc, scale, bias, w_dq);
      U gk[sub];
      if (QMV_NORMED) {
#pragma unroll
        for (int i = 0; i < sub; i++) {
          gk[i] = U(gain[k0 + i]) + QMV_GAIN_OFFSET;
        }
      }
#pragma unroll
      for (int v = 0; v < vecs_per_tg; v++) {
        const device T_act* xc = xv[v] + k0;
        U acc = 0;
#pragma unroll
        for (int i = 0; i < sub; i++) {
          U xi = static_cast<U>(xc[i]);
          if (QMV_NORMED) {
            sum_sq[v] += xi * xi;
            xi *= gk[i];
          }
          acc += xi * w_dq[i];
        }
        result[v] += acc;
      }
    }
  }

  // Reduce each vector's partial over its k_lanes with a shuffle ladder:
  // simd_sum would mix the results_per_simdgroup rows a simdgroup spans.
  for (int v = 0; v < vecs_per_tg; v++) {
    for (ushort d = k_lanes / 2; d >= 1; d >>= 1) {
      result[v] += simd_shuffle_down(result[v], d);
      if (QMV_NORMED) {
        sum_sq[v] += simd_shuffle_down(sum_sq[v], d);
      }
    }
  }

  if (k_lane == 0 && out_row < OUT_VEC_SIZE) {
    for (int v = 0; v < vecs_per_tg; v++) {
      if (vec0 + v < M) {
        const U s = qmv_row_scale(sum_sq[v], IN_VEC_SIZE);
        qmv_store<T_act>(y + (vec0 + v) * OUT_VEC_SIZE + out_row, result[v] * s, bias, out_row);
      }
    }
  }
}

// ─────────────────────────────────────────────────────────────────
// affine_qmv_wide — quantized.h:1723-1775. Non-batched only: the
// small-M band is a decode-batch shape, never an MoE weight batch.
// M is the bucket's, baked like K/N (slot 7, set only for this kernel).
// ─────────────────────────────────────────────────────────────────

SCRATCHY_CONSTANT_OPTIONAL(int, QMV_WIDE_M, 7);

template <
    typename T_act,
    typename T_scale,
    const int group_size,
    const int bits,
    int vecs_per_tg,
    int k_lanes>
[[kernel]] void affine_qmv_wide(
    const device uint32_t* w [[buffer(0)]],
    const device T_scale* scales [[buffer(1)]],
    const device T_scale* biases [[buffer(2)]],
    const device T_act* x [[buffer(3)]],
    device T_act* y [[buffer(4)]],
    const device T_scale* gain [[buffer(15)]],
    const device T_act* bias [[buffer(16)]],
    uint3 tid [[threadgroup_position_in_grid]],
    uint simd_gid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]]) {
  qmv_wide_impl<T_act, T_scale, group_size, bits, vecs_per_tg, k_lanes>(
      w,
      scales,
      biases,
      x,
      y,
      QMV_WIDE_M,
      tid,
      simd_gid,
      simd_lid,
      gain,
      bias);
}

// ─────────────────────────────────────────────────────────────────
// Instantiations — bits=4, gs in {32, 64, 128}, dtype in {f16, bf16}
// ─────────────────────────────────────────────────────────────────

#define INST_QMV_BATCHED(name, act_tag, act_type, scale_tag, scale_type, gs, bits, batched) \
  SCRATCHY_KERNEL(name##_##act_tag##_s_##scale_tag##_gs_##gs##_b_##bits##_batch_##batched,  \
                  name<act_type, scale_type, gs, bits, batched>)

#define INST_QMV_QUAD(name, act_tag, act_type, scale_tag, scale_type, gs, bits, D, batched) \
  SCRATCHY_KERNEL(                                                                          \
      name##_##act_tag##_s_##scale_tag##_gs_##gs##_b_##bits##_d_##D##_batch_##batched,      \
      name<act_type, scale_type, gs, bits, D, batched>)

#define INST_QMV_ALL(act_tag, act_type, scale_tag, scale_type, gs)                          \
  INST_QMV_BATCHED(affine_qmv_fast, act_tag, act_type, scale_tag, scale_type, gs, 4, 0)     \
  INST_QMV_BATCHED(affine_qmv_fast, act_tag, act_type, scale_tag, scale_type, gs, 4, 1)     \
  INST_QMV_BATCHED(affine_qmv,      act_tag, act_type, scale_tag, scale_type, gs, 4, 0)     \
  INST_QMV_BATCHED(affine_qmv,      act_tag, act_type, scale_tag, scale_type, gs, 4, 1)     \
  INST_QMV_QUAD(affine_qmv_quad,    act_tag, act_type, scale_tag, scale_type, gs, 4, 64, 0) \
  INST_QMV_QUAD(affine_qmv_quad,    act_tag, act_type, scale_tag, scale_type, gs, 4, 64, 1) \
  INST_QMV_QUAD(affine_qmv_quad,    act_tag, act_type, scale_tag, scale_type, gs, 4, 128,0) \
  INST_QMV_QUAD(affine_qmv_quad,    act_tag, act_type, scale_tag, scale_type, gs, 4, 128,1)

// Coverage: T_scale=half always (every sampled mlx-community 4bit ships
// F16 scales — `INT4_PARITY_PROBES.md:73,287`). T_act per `torch_dtype`.
// The `bfloat × bfloat` family that P1-P6 shipped (loader-cast F16→BF16)
// is removed here — that was the regression site `INT4_PARITY_PROBES.md`
// §7 `Decision: in-register cast` repays.
INST_QMV_ALL(f16,  half,   f16, half,    32)
INST_QMV_ALL(f16,  half,   f16, half,    64)
INST_QMV_ALL(f16,  half,   f16, half,   128)
INST_QMV_ALL(bf16, bfloat, f16, half,    32)
// bf16-scale instantiations — Qwen3-MoE (and any `torch_dtype: bfloat16`
// mlx-community 4bit) ships scales/biases as BF16, not F16. Matches
// MLX's `INSTANTIATE_QUANTIZED_FUNCTIONS(T_scale=bfloat16_t)` surface.
INST_QMV_ALL(bf16, bfloat, bf16, bfloat, 32)
INST_QMV_ALL(bf16, bfloat, bf16, bfloat, 64)
INST_QMV_ALL(bf16, bfloat, bf16, bfloat, 128)
INST_QMV_ALL(f16,  half,   bf16, bfloat, 32)
INST_QMV_ALL(f16,  half,   bf16, bfloat, 64)
INST_QMV_ALL(f16,  half,   bf16, bfloat, 128)

// 8-bit instantiations (Gemma4 MLP projections: 8-bit g64). The qmv /
// qdot template bodies are bits-generic (faithful MLX port — bits ∈
// {2,3,4,5,6,8} branches); only the entry-point symbols were 4-bit
// until now. bf16/bf16 = the Gemma4 production combo; f16/f16 kept
// for unit tests. batch_0 only (the decode/prefill paths never use
// the batched variants for the MLP).
#define INST_QMV_ALL_B8(act_tag, act_type, scale_tag, scale_type, gs)                       \
  INST_QMV_BATCHED(affine_qmv_fast, act_tag, act_type, scale_tag, scale_type, gs, 8, 0)     \
  INST_QMV_BATCHED(affine_qmv,      act_tag, act_type, scale_tag, scale_type, gs, 8, 0)     \
  INST_QMV_QUAD(affine_qmv_quad,    act_tag, act_type, scale_tag, scale_type, gs, 8, 64, 0) \
  INST_QMV_QUAD(affine_qmv_quad,    act_tag, act_type, scale_tag, scale_type, gs, 8, 128,0)

INST_QMV_ALL_B8(bf16, bfloat, bf16, bfloat, 64)
INST_QMV_ALL_B8(f16,  half,   f16,  half,   64)

// qmv_wide instantiations — the small-M band (2 ≤ M < vector_limit).
// k_lanes=8 (the affine pick, quantized.cpp:567): 4 output rows per
// simdgroup × 2 simdgroups = 8 rows per threadgroup. vecs_per_tg in
// {2,3,4,5} covers the decode buckets (bucket_m 2..8 on one tile, 16
// on 4×4). bits 4 and 8, gs 64 (the MLX-affine presets), batch_0
// only — the band is a decode-batch shape, never an MoE weight batch.
#define INST_QMV_WIDE(name, act_tag, act_type, scale_tag, scale_type, gs, bits, nv, kl)       \
  SCRATCHY_KERNEL(                                                                        \
      name##_##act_tag##_s_##scale_tag##_gs_##gs##_b_##bits##_nv_##nv##_kl_##kl##_batch_0, \
      name<act_type, scale_type, gs, bits, nv, kl>)

#define INST_QMV_WIDE_ALL(act_tag, act_type, scale_tag, scale_type, gs)              \
  INST_QMV_WIDE(affine_qmv_wide, act_tag, act_type, scale_tag, scale_type, gs, 4, 2, 8)  \
  INST_QMV_WIDE(affine_qmv_wide, act_tag, act_type, scale_tag, scale_type, gs, 4, 3, 8)  \
  INST_QMV_WIDE(affine_qmv_wide, act_tag, act_type, scale_tag, scale_type, gs, 4, 4, 8)  \
  INST_QMV_WIDE(affine_qmv_wide, act_tag, act_type, scale_tag, scale_type, gs, 4, 5, 8)  \
  INST_QMV_WIDE(affine_qmv_wide, act_tag, act_type, scale_tag, scale_type, gs, 8, 2, 8)  \
  INST_QMV_WIDE(affine_qmv_wide, act_tag, act_type, scale_tag, scale_type, gs, 8, 3, 8)  \
  INST_QMV_WIDE(affine_qmv_wide, act_tag, act_type, scale_tag, scale_type, gs, 8, 4, 8)  \
  INST_QMV_WIDE(affine_qmv_wide, act_tag, act_type, scale_tag, scale_type, gs, 8, 5, 8)

INST_QMV_WIDE_ALL(bf16, bfloat, f16, half, 64)
INST_QMV_WIDE_ALL(bf16, bfloat, bf16, bfloat, 64)
INST_QMV_WIDE_ALL(f16, half, f16, half, 64)

// ─────────────────────────────────────────────────────────────────
// nvfp4 CLEAN decode-matvec — FAITHFUL PORT of MLX `fp_qmv_impl`
// (mlx/backend/metal/kernels/fp_quantized.h). NVFP4 is NOT bolted onto
// the int4 `qmv_impl` via a template flag: MLX ships a *dedicated* fp4
// kernel, and reusing the int4 path drags the entire affine machinery
// (pre-division load, per-group bias, QuantizedBlockLoader) into the
// nvfp4 function as dead code that the MTL4 pipeline compiler
// miscompiles (MTL3 / unit tests are fine; MTL4 production = garbage).
// This standalone function has ZERO affine code. The only deviation
// from MLX is the scale: scratchy folds (e4m3·weight_scale_2) into one
// F16 per-group value at load, so we read `T_scale` directly instead of
// MLX's in-kernel `dequantize_scale<U,16>` fp8 path. group_size=16.
// ─────────────────────────────────────────────────────────────────

// nvfp4 dot — MLX `qdot` (fp4 branch): x·E2M1[code] over a group, ×scale,
// no bias. `w` holds 4 codes per uint16 (low→high nibble = elems 4i..4i+3).
template <typename U, int values_per_thread>
inline U nvfp4_qdot(const device uint8_t* w, const thread U* x_thread, U scale) {
  const device uint16_t* ws = (const device uint16_t*)w;
  U accum = 0;
  for (int i = 0; i < (values_per_thread / 4); i++) {
    uint16_t word = ws[i];
    accum += x_thread[4 * i] * nvfp4_decode(word & 0x000fu);
    accum += x_thread[4 * i + 1] * nvfp4_decode((uint(word) >> 4) & 0x000fu);
    accum += x_thread[4 * i + 2] * nvfp4_decode((uint(word) >> 8) & 0x000fu);
    accum += x_thread[4 * i + 3] * nvfp4_decode((uint(word) >> 12) & 0x000fu);
  }
  return scale * accum;
}

template <typename U, int values_per_thread>
inline U nvfp4_qdot_safe(
    const device uint8_t* w, const thread U* x_thread, U scale, int N) {
  const device uint16_t* ws = (const device uint16_t*)w;
  U accum = 0;
  for (int i = 0; i < (N / 4); i++) {
    uint16_t word = ws[i];
    accum += x_thread[4 * i] * nvfp4_decode(word & 0x000fu);
    accum += x_thread[4 * i + 1] * nvfp4_decode((uint(word) >> 4) & 0x000fu);
    accum += x_thread[4 * i + 2] * nvfp4_decode((uint(word) >> 8) & 0x000fu);
    accum += x_thread[4 * i + 3] * nvfp4_decode((uint(word) >> 12) & 0x000fu);
  }
  return scale * accum;
}

// Clean nvfp4 matvec — mirrors MLX `fp_qmv_impl` line-for-line.
template <typename T_act, typename T_scale, int group_size, int bits>
inline void fp_qmv_impl(
    const device uint32_t* w,
    const device T_scale* scales,
    const device T_act* x,
    device T_act* y,
    int in_vec_size,
    int out_vec_size,
    uint3 tid,
    uint simd_gid,
    uint simd_lid) {
  constexpr int num_simdgroups = 2;
  constexpr int results_per_simdgroup = 4;
  constexpr int packs_per_thread = 1;
  constexpr int pack_factor = get_pack_factor<bits, 32>();
  constexpr int bytes_per_pack = get_bytes_per_pack<bits, 32>();
  constexpr int values_per_thread = pack_factor * packs_per_thread;
  constexpr int block_size = values_per_thread * SIMD_SIZE;
  constexpr int scale_step_per_thread = group_size / values_per_thread;

  const device uint8_t* ws = (const device uint8_t*)w;
  typedef float U;
  thread U x_thread[values_per_thread];
  thread U result[results_per_simdgroup] = {0};

  const int in_vec_size_w = in_vec_size * bytes_per_pack / pack_factor;
  const int in_vec_size_g = in_vec_size / group_size;
  const int out_row = tid.y * (num_simdgroups * results_per_simdgroup) +
      simd_gid * results_per_simdgroup;
  const int used_out_row = min(out_vec_size - results_per_simdgroup, out_row);

  if (out_row >= out_vec_size) {
    return;
  }

  if (out_vec_size < (num_simdgroups * results_per_simdgroup)) {
    ws +=
        out_row * in_vec_size_w + simd_lid * packs_per_thread * bytes_per_pack;
    scales += out_row * in_vec_size_g + simd_lid / scale_step_per_thread;
    x += tid.x * in_vec_size + simd_lid * values_per_thread;
    y += tid.x * out_vec_size + out_row;

    int k = 0;
    for (; k < in_vec_size - block_size; k += block_size) {
      for (int i = 0; i < values_per_thread; i++) {
        x_thread[i] = x[i];
      }
      for (int row = 0;
           row < results_per_simdgroup && out_row + row < out_vec_size;
           row++) {
        auto wl = (const device uint8_t*)(ws + row * in_vec_size_w);
        const device T_scale* sl = scales + row * in_vec_size_g;
        U s = static_cast<U>(sl[0]);
        result[row] += nvfp4_qdot<U, values_per_thread>(wl, x_thread, s);
      }
      ws += block_size * bytes_per_pack / pack_factor;
      scales += block_size / group_size;
      x += block_size;
    }
    const int remaining = clamp(
        static_cast<int>(in_vec_size - k - simd_lid * values_per_thread),
        0,
        values_per_thread);
    if (remaining > 0) {
      for (int i = 0; i < remaining; i++) {
        x_thread[i] = x[i];
      }
      for (int i = remaining; i < values_per_thread; i++) {
        x_thread[i] = 0;
      }
      for (int row = 0;
           row < results_per_simdgroup && out_row + row < out_vec_size;
           row++) {
        auto wl = (const device uint8_t*)(ws + row * in_vec_size_w);
        const device T_scale* sl = scales + row * in_vec_size_g;
        U s = static_cast<U>(sl[0]);
        result[row] +=
            nvfp4_qdot_safe<U, values_per_thread>(wl, x_thread, s, remaining);
      }
    }
    for (int row = 0;
         row < results_per_simdgroup && out_row + row < out_vec_size;
         row++) {
      result[row] = simd_sum(result[row]);
      if (simd_lid == 0) {
        y[row] = static_cast<T_act>(result[row]);
      }
    }
  } else {
    ws += used_out_row * in_vec_size_w +
        simd_lid * packs_per_thread * bytes_per_pack;
    scales += used_out_row * in_vec_size_g + simd_lid / scale_step_per_thread;
    x += tid.x * in_vec_size + simd_lid * values_per_thread;
    y += tid.x * out_vec_size + used_out_row;

    int k = 0;
    for (; k < in_vec_size - block_size; k += block_size) {
      for (int i = 0; i < values_per_thread; i++) {
        x_thread[i] = x[i];
      }
      for (int row = 0; row < results_per_simdgroup; row++) {
        auto wl = (const device uint8_t*)(ws + row * in_vec_size_w);
        const device T_scale* sl = scales + row * in_vec_size_g;
        U s = static_cast<U>(sl[0]);
        result[row] += nvfp4_qdot<U, values_per_thread>(wl, x_thread, s);
      }
      ws += block_size * bytes_per_pack / pack_factor;
      scales += block_size / group_size;
      x += block_size;
    }
    const int remaining = clamp(
        static_cast<int>(in_vec_size - k - simd_lid * values_per_thread),
        0,
        values_per_thread);
    if (remaining > 0) {
      for (int i = 0; i < remaining; i++) {
        x_thread[i] = x[i];
      }
      for (int i = remaining; i < values_per_thread; i++) {
        x_thread[i] = 0;
      }
      for (int row = 0; row < results_per_simdgroup; row++) {
        auto wl = (const device uint8_t*)(ws + row * in_vec_size_w);
        const device T_scale* sl = scales + row * in_vec_size_g;
        U s = static_cast<U>(sl[0]);
        result[row] +=
            nvfp4_qdot_safe<U, values_per_thread>(wl, x_thread, s, remaining);
      }
    }
    for (int row = 0; row < results_per_simdgroup; row++) {
      result[row] = simd_sum(result[row]);
      if (simd_lid == 0) {
        y[row] = static_cast<T_act>(result[row]);
      }
    }
  }
}

// nvfp4_qmv entry — 5-buffer signature kept (w,scales,biases,x,y) so the
// binding/argument-table layout matches affine; `biases` is unused.
template <typename T_act, typename T_scale, const int group_size, const int bits>
[[kernel]] void nvfp4_qmv(
    const device uint32_t* w [[buffer(0)]],
    const device T_scale* scales [[buffer(1)]],
    const device T_scale* biases [[buffer(2)]],
    const device T_act* x [[buffer(3)]],
    device T_act* y [[buffer(4)]],
    uint3 tid [[threadgroup_position_in_grid]],
    uint simd_gid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]]) {
  fp_qmv_impl<T_act, T_scale, group_size, bits>(
      w, scales, x, y, IN_VEC_SIZE, OUT_VEC_SIZE, tid, simd_gid, simd_lid);
}

#define INST_NVFP4_QMV(act_tag, act_type, scale_tag, scale_type, gs)      \
  SCRATCHY_KERNEL(nvfp4_qmv_##act_tag##_s_##scale_tag##_gs_##gs##_b_4_batch_0, \
                  nvfp4_qmv<act_type, scale_type, gs, 4>)

// group_size is always 16 for NVFP4; T_scale always half (folded F16).
INST_NVFP4_QMV(f16, half, f16, half, 16)
INST_NVFP4_QMV(bf16, bfloat, f16, half, 16)

// ─────────────────────────────────────────────────────────────────
// affine_gather_qmv_{fast,} — quantized.h:1899-2021 (MoE rhs gather)
//
// SwitchGLU per-expert qmv. Each output row picks an expert via
// `rhs_indices[n * top_k + slot_k]`, offsets w/scales/biases into
// the expert's weight slab, and reuses qmv_fast_impl / qmv_impl
// for the actual matvec compute.
//
// Bindings (MLX gather kernels at quantized.h:1900 use 21-buffer
// layout for full broadcast support; we collapse to the SwitchGLU
// shape where x is [N, hidden] and rhs_indices is [N, top_k]):
//   buffer(0) = w           [num_experts, out_vec, in_vec/8]  uint32
//   buffer(1) = scales      [num_experts, out_vec, in_vec/gs] T_scale
//   buffer(2) = biases      [num_experts, out_vec, in_vec/gs] T_scale
//   buffer(3) = x           [N, in_vec]                       T_act
//   buffer(4) = rhs_indices [N, top_k]                        uint32
//   buffer(5) = y           [N, top_k, out_vec]               T_act
//
// IN_VEC_SIZE / OUT_VEC_SIZE are constant slots 0/1, compiled in just
// like the non-gather affine_qmv variants; GATHER_PER_ROW (slot 2,
// `AffineGatherQmvConstants`) is the output rows each x row feeds.
//
// Dispatch: tid.x = 0 (we feed the broadcast token via z-axis),
// tid.y = output-block-row index, tid.z = n * top_k + slot_k. The
// non-gather qmv_*_impl reads `tid.x * in_vec_size` from x and
// `tid.x * out_vec_size` from y, so pinning tid.x=0 and pre-
// offsetting both pointers is identical to a single-batch matvec.
//
// The MoE block's fused kernels run the same per-pair matvec:
//   affine_gather_qmv_gated{_fast}   gate and up of one output block, then act(gate) * up
//   affine_gather_qmv_combine{_fast} down of one 4-row group for every chosen expert, then
//                                    the weighted combine of those rows
// ─────────────────────────────────────────────────────────────────

#ifdef SCRATCHY_CONSTANT_3
// The gated activation (slot 3): 0 SiLU, 1 GELU (tanh).
SCRATCHY_CONSTANT(int, GATED_ACT, 3);

// A dense gated MLP's gate and up projections and its activation, one row:
// `y = act(gate · x) * (up · x)`, as the two matvecs and `silu_mul` / `gelu_mul` compute it.
//   buffer(0-2) = gate w / scales / biases   buffer(5-7) = up w / scales / biases
//   buffer(3)   = x  [in_vec]                buffer(4)   = y  [out_vec]
// Dispatch (1, out_vec / 8, 1), threadgroup (32, 4, 1): simdgroups 0-1 run the gate matvec over
// the 8-row block tid.y, 2-3 the up matvec's, each row rounded to T_act as the matvec stores it;
// then lanes 0-7 of simdgroup 0 apply the activation to the block's rows.
template <typename T_act, typename T_scale, int group_size, int bits, bool fast>
[[kernel]] void affine_qmv_gated(
    const device uint32_t* gate_w      [[buffer(0)]],
    const device T_scale*  gate_scales [[buffer(1)]],
    const device T_scale*  gate_biases [[buffer(2)]],
    const device T_act*    x           [[buffer(3)]],
    device T_act*          y           [[buffer(4)]],
    const device uint32_t* up_w        [[buffer(5)]],
    const device T_scale*  up_scales   [[buffer(6)]],
    const device T_scale*  up_biases   [[buffer(7)]],
    const device T_scale*  gain        [[buffer(15)]],
    uint3 tid       [[threadgroup_position_in_grid]],
    uint  simd_gid  [[simdgroup_index_in_threadgroup]],
    uint  simd_lid  [[thread_index_in_simdgroup]]) {
  static_assert(OUT_VEC_SIZE % 8 == 0, "every block holds 8 whole rows");
  threadgroup T_act rows[2][8];
  const bool up = simd_gid >= 2;
  // The block's own rows: the impl then reads rows 0-7 of a matrix that starts at the block.
  const size_t row0 = size_t(tid.y) * 8;
  const size_t w_offset = row0 * size_t(IN_VEC_SIZE) * bits / 32;
  const size_t sb_offset = row0 * size_t(IN_VEC_SIZE / group_size);
  const device uint32_t* w = (up ? up_w : gate_w) + w_offset;
  const device T_scale* scales = (up ? up_scales : gate_scales) + sb_offset;
  const device T_scale* biases = (up ? up_biases : gate_biases) + sb_offset;
  if (fast) {
    qmv_fast_impl<T_act, T_scale, group_size, bits, threadgroup T_act*>(
        w, scales, biases, x, rows[up], IN_VEC_SIZE, 8, uint3(0), simd_gid % 2, simd_lid, gain);
  } else {
    qmv_impl<T_act, T_scale, group_size, bits, threadgroup T_act*>(
        w, scales, biases, x, rows[up], IN_VEC_SIZE, 8, uint3(0), simd_gid % 2, simd_lid, gain);
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);
  if (simd_gid == 0 && simd_lid < 8) {
    float g = float(rows[0][simd_lid]);
    float u = float(rows[1][simd_lid]);
    y[row0 + simd_lid] = static_cast<T_act>(GATED_ACT == 1 ? gelu_mul_f(g, u) : silu_mul_f(g, u));
  }
}
#endif

#ifdef SCRATCHY_CONSTANT_2
SCRATCHY_CONSTANT(int, GATHER_PER_ROW, 2);

// 13-17 (`MetalFusion::MoeRouted`): the gated kernel routes its token itself, from the router
// logits, by `moe_route.h`'s program — the experts (13), a softmax over them first (14), the
// scores' scale (15), their last step (16: 1 softmax, 2 renorm) and the per-expert scale (17) —
// and stores the picks and scores the later kernels read. Unset: they are the routing command's.
SCRATCHY_CONSTANT_OPTIONAL(int, ROUTED_EXPERTS, 13);
SCRATCHY_CONSTANT_OPTIONAL(bool, ROUTED_PRE, 14);
SCRATCHY_CONSTANT_OPTIONAL(float, ROUTED_SCALE, 15);
SCRATCHY_CONSTANT_OPTIONAL(int, ROUTED_POST, 16);
SCRATCHY_CONSTANT_OPTIONAL(bool, ROUTED_EXPERT_SCALE, 17);
constant constexpr bool ROUTED = ROUTED_EXPERTS_SET;
// The experts a routed kernel's top-k reads (a valid shape when unrouted).
constant constexpr int ROUTED_E = ROUTED ? ROUTED_EXPERTS : 32;
constant constexpr bool ROUTED_SOFT = ROUTED && ROUTED_PRE;

// Pair `nk`'s matvec over expert `expert_idx`'s weights: output block `block` (8 rows, 4 per
// simdgroup) of `y`'s row `nk`, reading `x`'s row `x_row` — normalized by `gain` as it loads,
// under QMV_NORMED (`MetalFusion::NormedQmv`: the MoE block's input norm, folded in).
template <typename T_act, typename T_scale, int group_size, int bits, bool fast>
METAL_FUNC void gather_qmv_pair(
    const device uint32_t* w,
    const device T_scale*  scales,
    const device T_scale*  biases,
    const device T_act*    x,
    uint expert_idx,
    device T_act*          y,
    uint nk,
    uint x_row,
    uint block,
    uint simd_gid,
    uint simd_lid,
    const device T_scale*  gain) {

  // Per-expert weight slab strides: w is packed int4 with
  // `in_vec/8 * out_vec` uint32 per expert; scales/biases hold
  // `in_vec/gs * out_vec` per expert.
  size_t expert_stride_w = size_t(IN_VEC_SIZE / (32 / bits)) * size_t(OUT_VEC_SIZE);
  size_t expert_stride_sb = size_t(IN_VEC_SIZE / group_size) * size_t(OUT_VEC_SIZE);
  const device uint32_t* w_e = w + expert_idx * expert_stride_w;
  const device T_scale*  s_e = scales + expert_idx * expert_stride_sb;
  const device T_scale*  b_e = biases + expert_idx * expert_stride_sb;
  const device T_act*    x_e = x + size_t(x_row) * size_t(IN_VEC_SIZE);
  device T_act*          y_e = y + size_t(nk) * size_t(OUT_VEC_SIZE);

  uint3 inner_tid = uint3(0, block, 0);
  if (fast) {
    qmv_fast_impl<T_act, T_scale, group_size, bits>(
        w_e, s_e, b_e, x_e, y_e, IN_VEC_SIZE, OUT_VEC_SIZE,
        inner_tid, simd_gid, simd_lid, gain);
  } else {
    qmv_impl<T_act, T_scale, group_size, bits>(
        w_e, s_e, b_e, x_e, y_e, IN_VEC_SIZE, OUT_VEC_SIZE,
        inner_tid, simd_gid, simd_lid, gain);
  }
}

template <typename T_act, typename T_scale, int group_size, int bits, bool fast>
[[kernel]] void affine_gather_qmv(
    const device uint32_t* w           [[buffer(0)]],
    const device T_scale*  scales      [[buffer(1)]],
    const device T_scale*  biases      [[buffer(2)]],
    const device T_act*    x           [[buffer(3)]],
    const device uint32_t* rhs_indices [[buffer(4)]],
    device T_act*          y           [[buffer(5)]],
    const device T_scale*  gain        [[buffer(15)]],
    uint3 tid       [[threadgroup_position_in_grid]],
    uint  simd_gid  [[simdgroup_index_in_threadgroup]],
    uint  simd_lid  [[thread_index_in_simdgroup]]) {
  // `tid.z` flattens the (token, top_k_slot) axis. tid.x is fixed
  // to 0 — the M-axis broadcast is folded into z.
  uint nk = tid.z;
  gather_qmv_pair<T_act, T_scale, group_size, bits, fast>(
      w, scales, biases, x, rhs_indices[nk], y, nk, nk / uint(GATHER_PER_ROW), tid.y,
      simd_gid, simd_lid, gain);
}

#ifdef SCRATCHY_CONSTANT_3
// Where the pairs that share an expert run (slot 4, `SharedExperts`): 0 each in its own
// threadgroup, 1 one after another in the threadgroup of the expert's first pair.
SCRATCHY_CONSTANT(int, GATHER_SHARED_EXPERTS, 4);

// The MoE block's gate and up projections and its gated activation: `gate_y` ends holding
// `act(gate) * up` for every chosen expert's rows. Each token's row feeds GATHER_PER_ROW
// (top-k) pairs.
//   buffer(0-2) = gate w / scales / biases   buffer(6-8) = up w / scales / biases
//   buffer(3)   = x                          buffer(9)   = up y  [N, top_k, out_vec]
//   buffer(4)   = rhs_indices                buffer(10)  = router logits, routed
//   buffer(5)   = gate y                     buffer(11)  = scores [N, top_k], routed
//                 [N, top_k, out_vec]        buffer(12)  = per-expert scales, routed and scaled
//                                            buffer(15)  = the norm's gain, normed
// Dispatch (N * top_k, ceil(out_vec / 8), 1), threadgroup (32, 4, 1): pair tid.x; simdgroups
// 0-1 run the gate matvec's 8-row block tid.y, 2-3 the up matvec's, then lanes 0-7 of simdgroup 0
// apply the activation to the block's rows. In a verify bucket (GATHER_SHARED_EXPERTS) the pairs
// that share an expert run in the threadgroup of the expert's first pair, one after another: the
// expert's block is read from memory once and then from cache, each pair's matvec the same as
// alone; the expert's other threadgroups exit. A pair scans the pairs before it, so a bucket of
// many pairs runs each in its own threadgroup. Every gate row is written raw only by the threadgroup that
// then activates it: an out_vec with a 1-3 row tail block would have qmv_impl redo the previous
// block's last rows there, raw, so this kernel takes an out_vec that is a multiple of 4. Routed,
// the threadgroup picks its token's experts first (E <= 512: its 128 threads cover E / 4), and
// the token's first threadgroup stores the picks into rhs_indices and their scores.
template <typename T_act, typename T_scale, int group_size, int bits, bool fast>
[[kernel]] void affine_gather_qmv_gated(
    const device uint32_t* gate_w      [[buffer(0)]],
    const device T_scale*  gate_scales [[buffer(1)]],
    const device T_scale*  gate_biases [[buffer(2)]],
    const device T_act*    x           [[buffer(3)]],
    device uint32_t*       rhs_indices [[buffer(4)]],
    device T_act*          gate_y      [[buffer(5)]],
    const device uint32_t* up_w        [[buffer(6)]],
    const device T_scale*  up_scales   [[buffer(7)]],
    const device T_scale*  up_biases   [[buffer(8)]],
    device T_act*          up_y        [[buffer(9)]],
    const device T_act*    logits      [[buffer(10)]],
    device T_act*          scores      [[buffer(11)]],
    const device T_act*    expert_scale [[buffer(12)]],
    const device T_scale*  gain        [[buffer(15)]],
    uint3 tid       [[threadgroup_position_in_grid]],
    uint3 grid      [[threadgroups_per_grid]],
    uint  simd_gid  [[simdgroup_index_in_threadgroup]],
    uint  simd_lid  [[thread_index_in_simdgroup]]) {
  static_assert(OUT_VEC_SIZE % 4 == 0, "a 1-3 row tail block would race the activation");
  static_assert(!ROUTED || ROUTED_E <= 4 * 128, "a routed softmax row's threads cover E / 4");
  static_assert(!ROUTED || GATHER_SHARED_EXPERTS == 0, "a routed pair knows its own token's picks only");
  bool up = simd_gid >= 2;
  threadgroup uint routed[GATHER_PER_ROW];
  threadgroup T_act soft[ROUTED_SOFT ? ROUTED_E : 1];
  threadgroup float local_a[32];
  threadgroup float local_b[32];
  uint expert;
  uint end = tid.x + 1;
  if (ROUTED) {
    const uint nk = tid.x;
    const uint n = nk / uint(GATHER_PER_ROW), lid = simd_gid * 32 + simd_lid;
    const device T_act* row = logits + size_t(n) * ROUTED_E;
    route_top_k<T_act, ROUTED_E, GATHER_PER_ROW, ROUTED_SOFT>(
        row, soft, routed, lid, simd_lid, simd_gid, local_a, local_b);
    expert = routed[nk % uint(GATHER_PER_ROW)];
    if (tid.y == 0 && nk % uint(GATHER_PER_ROW) == 0) {
      device T_act* row_scores = scores + size_t(n) * GATHER_PER_ROW;
      route_scores<T_act, GATHER_PER_ROW, ROUTED_SOFT, ROUTED_SCALE_SET, ROUTED_POST,
                   ROUTED_EXPERT_SCALE>(row, soft, routed, row_scores, expert_scale,
                                        ROUTED_SCALE, lid, simd_lid, simd_gid, local_a, local_b);
      if (lid < uint(GATHER_PER_ROW)) {
        rhs_indices[size_t(n) * GATHER_PER_ROW + lid] = routed[lid];
      }
    }
  } else {
    expert = rhs_indices[tid.x];
    if (GATHER_SHARED_EXPERTS == 1) {
      for (uint q = 0; q < tid.x; q++) {
        if (rhs_indices[q] == expert) {
          return;
        }
      }
      end = grid.x;
    }
  }
  uint row = tid.y * 8 + simd_lid;
  // The threadgroup's own pair, then (shared) the later pairs of its expert. A routed pair's
  // index is its own pick: the token's first threadgroup may still be storing rhs_indices.
  for (uint nk = tid.x; nk < end; nk++) {
    if (nk != tid.x && rhs_indices[nk] != expert) {
      continue;
    }
    gather_qmv_pair<T_act, T_scale, group_size, bits, fast>(
        up ? up_w : gate_w, up ? up_scales : gate_scales, up ? up_biases : gate_biases, x,
        expert, up ? up_y : gate_y, nk, nk / uint(GATHER_PER_ROW), tid.y, simd_gid % 2,
        simd_lid, gain);
    threadgroup_barrier(mem_flags::mem_device);
    if (simd_gid == 0 && simd_lid < 8 && row < uint(OUT_VEC_SIZE)) {
      size_t at = size_t(nk) * size_t(OUT_VEC_SIZE) + row;
      float g = float(gate_y[at]);
      float u = float(up_y[at]);
      gate_y[at] = static_cast<T_act>(GATED_ACT == 1 ? gelu_mul_f(g, u) : silu_mul_f(g, u));
    }
  }
}
#endif

// 18 / 19: what the combine computes as it stores each row (`MetalFusion::CombineEpilogue`) —
// 18 the shared expert's rows (buffer 8) scaled by σ of the token's gate (buffer 9) added, as
// `gate_scale` computes them; 19 the residual add into `out`, as `residual_add` adds it.
SCRATCHY_CONSTANT_OPTIONAL(bool, COMBINE_GATE_SCALE_FC, 18);
SCRATCHY_CONSTANT_OPTIONAL(bool, COMBINE_RESIDUAL_FC, 19);
constant constexpr bool COMBINE_GATE_SCALE = COMBINE_GATE_SCALE_FC_SET && COMBINE_GATE_SCALE_FC;
constant constexpr bool COMBINE_RESIDUAL = COMBINE_RESIDUAL_FC_SET && COMBINE_RESIDUAL_FC;

// The MoE block's down projection and its weighted combine:
// `out[n, d] = Σ_k down[n, k, d] · scores[n, k]`, summed in slot order (as `moe_weighted_sum`).
// Each pair's x row is its own (the gated activation's rows); GATHER_PER_ROW is the top-k.
//   buffer(0-2) = w / scales / biases   buffer(5) = y      [N, top_k, out_vec]
//   buffer(3)   = x  [N, top_k, in_vec] buffer(6) = scores [N, top_k]
//   buffer(4)   = rhs_indices           buffer(7) = out    [N, out_vec]
// Dispatch (N, ceil(out_vec / 4), 1), threadgroup (32, top_k, 1): token n = tid.x (fastest, as
// the gated kernel's pairs); simdgroup k runs pair (n, k)'s matvec over the 4-row group tid.y, then lanes 0-3 of simdgroup 0 combine the group's
// rows. A pair row another threadgroup also writes (qmv_impl redoing a tail) gets the same bits.
template <typename T_act, typename T_scale, int group_size, int bits, bool fast>
[[kernel]] void affine_gather_qmv_combine(
    const device uint32_t* w           [[buffer(0)]],
    const device T_scale*  scales      [[buffer(1)]],
    const device T_scale*  biases      [[buffer(2)]],
    const device T_act*    x           [[buffer(3)]],
    const device uint32_t* rhs_indices [[buffer(4)]],
    device T_act*          y           [[buffer(5)]],
    const device T_act*    scores      [[buffer(6)]],
    device T_act*          out         [[buffer(7)]],
    const device T_act*    shared_y    [[buffer(8)]],
    const device T_act*    gate        [[buffer(9)]],
    uint3 tid       [[threadgroup_position_in_grid]],
    uint  simd_gid  [[simdgroup_index_in_threadgroup]],
    uint  simd_lid  [[thread_index_in_simdgroup]]) {
  uint n = tid.x;
  uint nk = n * uint(GATHER_PER_ROW) + simd_gid;
  gather_qmv_pair<T_act, T_scale, group_size, bits, fast>(
      w, scales, biases, x, rhs_indices[nk], y, nk, nk, tid.y / 2, tid.y % 2, simd_lid,
      nullptr);
  threadgroup_barrier(mem_flags::mem_device);
  uint row = tid.y * 4 + simd_lid;
  if (simd_gid == 0 && simd_lid < 4 && row < uint(OUT_VEC_SIZE)) {
    const device T_act* rows = y + size_t(n) * uint(GATHER_PER_ROW) * size_t(OUT_VEC_SIZE);
    float acc = 0.0f;
    for (int k = 0; k < GATHER_PER_ROW; ++k) {
      acc = fma(float(rows[size_t(k) * size_t(OUT_VEC_SIZE) + row]),
                float(scores[n * uint(GATHER_PER_ROW) + uint(k)]),
                acc);
    }
    const size_t at = size_t(n) * size_t(OUT_VEC_SIZE) + row;
    T_act v = T_act(acc);
    if (COMBINE_GATE_SCALE) {
      v = T_act(gate_scale_f(float(v), float(shared_y[at]), float(gate[n])));
    }
    if (COMBINE_RESIDUAL) {
      v = out[at] + v;
    }
    out[at] = v;
  }
}
#endif

#define INST_GATHER_QMV(name, act_tag, act_type, scale_tag, scale_type, gs, bits)               \
  SCRATCHY_KERNEL(name##_fast_##act_tag##_s_##scale_tag##_gs_##gs##_b_##bits,                   \
                  name<act_type, scale_type, gs, bits, true>)                                   \
  SCRATCHY_KERNEL(name##_##act_tag##_s_##scale_tag##_gs_##gs##_b_##bits,                        \
                  name<act_type, scale_type, gs, bits, false>)

#define INST_GATHER_QMV_ALL(act_tag, act_type, scale_tag, scale_type, gs) \
  INST_GATHER_QMV(affine_gather_qmv,         act_tag, act_type, scale_tag, scale_type, gs, 4) \
  INST_GATHER_QMV(affine_gather_qmv_gated,   act_tag, act_type, scale_tag, scale_type, gs, 4) \
  INST_GATHER_QMV(affine_qmv_gated,          act_tag, act_type, scale_tag, scale_type, gs, 4) \
  INST_GATHER_QMV(affine_gather_qmv_combine, act_tag, act_type, scale_tag, scale_type, gs, 4)

INST_GATHER_QMV_ALL(f16,  half,   f16, half,    32)
INST_GATHER_QMV_ALL(f16,  half,   f16, half,    64)
INST_GATHER_QMV_ALL(f16,  half,   f16, half,   128)
INST_GATHER_QMV_ALL(bf16, bfloat, f16, half,    32)
INST_GATHER_QMV_ALL(bf16, bfloat, f16, half,    64)
INST_GATHER_QMV_ALL(bf16, bfloat, f16, half,   128)
// bf16-scale variants — see the bf16-scale block under INST_QMV_ALL.
INST_GATHER_QMV_ALL(bf16, bfloat, bf16, bfloat, 32)
INST_GATHER_QMV_ALL(bf16, bfloat, bf16, bfloat, 64)
INST_GATHER_QMV_ALL(bf16, bfloat, bf16, bfloat, 128)
INST_GATHER_QMV_ALL(f16,  half,   bf16, bfloat, 32)
INST_GATHER_QMV_ALL(f16,  half,   bf16, bfloat, 64)
INST_GATHER_QMV_ALL(f16,  half,   bf16, bfloat, 128)

// 8-bit gather-qmv (MoE decode) for MLX-native mixed/dynamic quant
// (OptiQ), whose sensitive edge layers ship 8-bit switch_glu experts.
#define INST_GATHER_QMV_ALL_B8(act_tag, act_type, scale_tag, scale_type, gs) \
  INST_GATHER_QMV(affine_gather_qmv,         act_tag, act_type, scale_tag, scale_type, gs, 8) \
  INST_GATHER_QMV(affine_gather_qmv_gated,   act_tag, act_type, scale_tag, scale_type, gs, 8) \
  INST_GATHER_QMV(affine_qmv_gated,          act_tag, act_type, scale_tag, scale_type, gs, 8) \
  INST_GATHER_QMV(affine_gather_qmv_combine, act_tag, act_type, scale_tag, scale_type, gs, 8)
INST_GATHER_QMV_ALL_B8(f16,  half,   f16, half,    32)
INST_GATHER_QMV_ALL_B8(f16,  half,   f16, half,    64)
INST_GATHER_QMV_ALL_B8(f16,  half,   f16, half,   128)
INST_GATHER_QMV_ALL_B8(bf16, bfloat, f16, half,    32)
INST_GATHER_QMV_ALL_B8(bf16, bfloat, f16, half,    64)
INST_GATHER_QMV_ALL_B8(bf16, bfloat, f16, half,   128)
INST_GATHER_QMV_ALL_B8(bf16, bfloat, bf16, bfloat, 32)
INST_GATHER_QMV_ALL_B8(bf16, bfloat, bf16, bfloat, 64)
INST_GATHER_QMV_ALL_B8(bf16, bfloat, bf16, bfloat, 128)
INST_GATHER_QMV_ALL_B8(f16,  half,   bf16, bfloat, 32)
INST_GATHER_QMV_ALL_B8(f16,  half,   bf16, bfloat, 64)
INST_GATHER_QMV_ALL_B8(f16,  half,   bf16, bfloat, 128)
INST_QMV_ALL(bf16, bfloat, f16, half,  64)
INST_QMV_ALL(bf16, bfloat, f16, half, 128)
